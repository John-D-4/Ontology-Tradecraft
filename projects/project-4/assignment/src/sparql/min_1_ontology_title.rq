# Title: 
#     Ontology Title Required
# Constraint Description: 
#     Any owl:Ontology must have an rdfs:label.
# Severity:
#     Warning

PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX obo: <http://purl.obolibrary.org/obo/>

# Find all ontologies that lack labels
SELECT DISTINCT ?ont
WHERE {
  ?ont rdf:type owl:Ontology .
  
  # Check that ontology does NOT have a valid label
  FILTER NOT EXISTS {
    ?ont rdfs:label ?label .
    FILTER (!isBlank(?label))
    FILTER (STR(?label) != "")
  }
}
ORDER BY ?ont
